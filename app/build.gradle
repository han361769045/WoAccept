apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
def releaseTime() {
    return new Date().format("yyyy-MM-dd-HH", TimeZone.getDefault())
}
android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.neusoft.woaccept"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode project.VERSION_CODE as int
        versionName project.VERSION_NAME
    }
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword properties.getProperty("keyPassword")
//            storeFile file('/debug.jks')
            storeFile project.rootProject.file('debug.jks')
            storePassword properties.getProperty("storePassword")
        }
        release {
            keyAlias 'debug'
            keyPassword properties.getProperty("keyPassword")
//            storeFile file('/debug.jks')
            storeFile project.rootProject.file('debug.jks')
            storePassword properties.getProperty("storePassword")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".test"
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        XA {
            buildConfigField "String", "ROOT_URL", '"http://130.84.1.100:8090/crm4/rest/"'
            applicationIdSuffix ".xa"
            manifestPlaceholders = [label: "西安沃受理"]
        }
        SC {
            applicationIdSuffix ".sc"
            buildConfigField "String", "ROOT_URL", '"http://130.84.1.100:8090/crm4/rest/"'
            manifestPlaceholders = [label: "四川沃受理"]
        }
    }
    //将release版本的包名重命名，加上版本及日期
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                def fileName = "${variant.productFlavors[0].name}_V${defaultConfig.versionName + "_" + defaultConfig.versionCode}_${releaseTime()}release.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}
apt {
    arguments {
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
        trace 'true'
        resourcePackageName 'com.neusoft.woaccept'
        logLevel 'ERROR'
        logAppenderConsole 'true'
        classSuffix '_'
        //classSuffix is used to change the suffix of enhanced classes like Activities with @EActivity. The default value is _.
        generateFinalClasses 'true'
        //generateFinalClasses is used to set if generated classes should be final or not. The default value is true.
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.androidannotations:androidannotations-api:${ANDROIDANNOTATIONS_VERSION}"
    apt "org.androidannotations:androidannotations:${ANDROIDANNOTATIONS_VERSION}"
    apt "org.androidannotations:otto:${ANDROIDANNOTATIONS_VERSION}"
    compile "org.androidannotations:rest-spring-api:${ANDROIDANNOTATIONS_VERSION}"
    apt "org.androidannotations:rest-spring:${ANDROIDANNOTATIONS_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIBRARY}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY}"
    compile "com.android.support:design:${SUPPORT_LIBRARY}"
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M3'
    compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'com.daimajia.slider:library:1.1.5@aar'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.leo.lu:llrecyclerview:1.0.0'
}
